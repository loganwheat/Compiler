import java_cup.runtime.*;

terminal CLASS;
terminal FINAL;
terminal VOID;
terminal INT;
terminal CHAR;
terminal BOOL;
terminal FLOAT;
terminal IF;
terminal FI;
terminal WHILE;
terminal RETURN;
terminal ELSE;
terminal TRUE;
terminal FALSE;
terminal READ;
terminal PRINT;
terminal PRINTL;

terminal SEMI;
terminal COMMA;
terminal OPENP;
terminal CLOSEP;
terminal OPENSB;
terminal CLOSESB;
terminal OPENCB;
terminal CLOSECB;
terminal TILDE;
terminal ASSMNT;
terminal MULT;
terminal DIV;
terminal PLUS;
terminal MINUS;
terminal LESSTHAN;
terminal GREATERTHAN;
terminal LEQ;
terminal GEQ;
terminal EQUALTO;
terminal ANGLEB;
terminal OR;
terminal AND;
terminal INCREMENT;
terminal DECREMENT;
terminal QUESTIONMARK;
terminal COLON;

terminal String INTLIT;
terminal String ID;
terminal String CHARLIT;
terminal String FLOATLIT;
terminal String STRLIT;

//----------------

non terminal Program program;
non terminal Memberdecls memberdecls;
non terminal Fieldsmethods fieldsmethods;
non terminal Fielddecls fielddecls;
non terminal Methoddecls methoddecls;
non terminal Fielddecl fielddecl;
non terminal Optionalexpr optionalexpr;
non terminal Methoddecl methoddecl;
non terminal Optionalsemi optionalsemi;
non terminal Type type;
non terminal Argdecls argdecls;
non terminal ArgdeclList argdeclList;
non terminal Argdecl argdecl;
non terminal Stmts stmts;
non terminal Stmt stmt;
non terminal IfEnd ifEnd;
non terminal Name name;
non terminal Args args;
non terminal Readlist readlist;
non terminal Printlist printlist;
non terminal Printlinelist printlinelist;
non terminal Expr expr;
non terminal Binaryop binaryop;

precedence left COMMA;
precedence right ASSMNT;
precedence left OR;
precedence left AND;
precedence left EQUALTO, ANGLEB;
precedence left LESSTHAN, LEQ, GREATERTHAN, GEQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;

program ::=
		CLASS ID:i OPENCB memberdecls:md CLOSECB
		{: RESULT = new Program(i, md); :}
		;

memberdecls ::=
		fieldsmethods:fm
		{: RESULT = new Memberdecls(fm); :}
		;

fieldsmethods ::=
		fielddecl:f fieldsmethods:fm
		{: RESULT = new Fieldsmethods(f, fm); :}
		|
		methoddecl:md methoddecls:mds
		{: RESULT = new Fieldsmethods(md, mds); :}
		|
		{: RESULT = new Fieldsmethods(); :}
		;

/*
memberdecls ::=
	    fielddecls:fds methoddecls:md
	    {: RESULT = new Memberdecls(fds, md); :}
		|
		fielddecl:fd methoddecls:md
		{: RESULT = new Memberdecls(fd, md); :}
		|
		methoddecls:md
		{: RESULT = new Memberdecls(md); :}
	    ;
*/

fielddecls ::=
	   	fielddecl:fd fielddecls:fds
	   	{: RESULT = new Fielddecls(fd, fds); :}
		|
		{: RESULT = new Fielddecls(); :}
	   	;

methoddecls ::=
	    methoddecl:md methoddecls:mds
	    {: RESULT = new Methoddecls(md, mds); :}
	    |
	    {: RESULT = new Methoddecls(); :}
	    ;

fielddecl ::=
		FINAL type:t ID:i optionalexpr:oe SEMI
		{: RESULT = new Fielddecl(true, t, i, oe); :}
		|
	  	type:t ID:i optionalexpr:oe SEMI
	  	{: RESULT = new Fielddecl(false, t, i, oe); :}
	  	|
	  	type:t ID:i OPENSB INTLIT:il CLOSESB SEMI
	  	{: RESULT = new Fielddecl(t, i, il); :}
	  	;

optionalexpr ::=
		ASSMNT expr:e
		{: RESULT = new Optionalexpr(e); :}
		|
		{: RESULT = new Optionalexpr(); :}
		;

methoddecl ::=
		type:t ID:i OPENP argdecls:a CLOSEP OPENCB fielddecls:fds stmts:s CLOSECB optionalsemi:os
		{: RESULT = new Methoddecl(t, i, a, fds, s, os); :}
		|
		VOID ID:i OPENP argdecls:a CLOSEP OPENCB fielddecls:fds stmts:s CLOSECB optionalsemi:os
		{: RESULT = new Methoddecl(true, i, a, fds, s, os); :}
		;

optionalsemi ::=
		SEMI
		{: RESULT = new Optionalsemi(";"); :}
		|
		{: RESULT = new Optionalsemi(); :}
		;

type ::=
		INT
		{: RESULT = new Type(1); :}
		|
		CHAR
		{: RESULT = new Type(2); :}
		|
		BOOL
		{: RESULT = new Type(3); :}
		|
		FLOAT
		{: RESULT = new Type(4); :}
		;

argdecls ::=
		argdeclList:al
		{: RESULT = new Argdecls(al); :}
		|
		{: RESULT = new Argdecls(); :}
		;

argdeclList ::=
		argdecl:a COMMA argdeclList:al
		{: RESULT = new ArgdeclList(a, al); :}
		|
		argdecl:a
		{: RESULT = new ArgdeclList(a); :}
		;

argdecl ::=
		type:t ID:i
		{: RESULT = new Argdecl(false, t, i); :}
		|
		type:t ID:i OPENSB CLOSESB
		{: RESULT = new Argdecl(true, t, i); :}
		;

stmts ::=
		stmt:s stmts:ss
		{: RESULT = new Stmts(s, ss); :}
		|
		{: RESULT = new Stmts(); :}
		;

stmt ::= 
		IF OPENP expr:e CLOSEP stmt:s ifEnd:ie
		{: RESULT = new Stmt(1, e, s, ie); :}
		|
		WHILE OPENP expr:e CLOSEP stmt:s
		{: RESULT = new Stmt(2, e, s); :}
		|
		name:n ASSMNT expr:e SEMI
		{: RESULT = new Stmt(3, n, e); :}
		|
		READ OPENP readlist:rl CLOSEP SEMI
		{: RESULT = new Stmt(4, rl); :}
		|
		PRINT OPENP printlist:pl CLOSEP SEMI
		{: RESULT = new Stmt(5, pl); :}
		|
		PRINTL OPENP printlinelist:pll CLOSEP SEMI
		{: RESULT = new Stmt(6, pll); :}
		|
		ID:i OPENP CLOSEP SEMI
		{: RESULT = new Stmt(7, i); :}
		|
		ID:i OPENP args:a CLOSEP SEMI
		{: RESULT = new Stmt(8, i, a); :}
		|
		RETURN SEMI
		{: RESULT = new Stmt(9); :}
		|
		RETURN expr:e SEMI
		{: RESULT = new Stmt(10, e); :}
		|
		name:n INCREMENT SEMI
		{: RESULT = new Stmt(11, n); :}
		|
		name:n DECREMENT SEMI
		{: RESULT = new Stmt(12, n); :}
		|
		OPENCB fielddecls:fds stmts:s CLOSECB optionalsemi:os
		{: RESULT = new Stmt(13, fds, s, os); :}
		;

ifEnd ::=
		ELSE stmt:s FI
		{: RESULT = new IfEnd(s); :}
		|
		FI
		{: RESULT = new IfEnd(); :}
		;

name ::=
		ID:i
		{: RESULT = new Name(i); :}
		|
		ID:i OPENSB expr:e CLOSESB
		{: RESULT = new Name(i, e); :}
		;

args ::=
		expr:e COMMA args:a
		{: RESULT = new Args(e, a); :}
		|
		expr:e
		{: RESULT = new Args(e); :}
		;

readlist ::=
		name:n COMMA args:a
		{: RESULT = new Readlist(n, a); :}
		|
		name:n
		{: RESULT = new Readlist(n); :}
		;

printlist ::=
		expr:e COMMA readlist:rl
		{: RESULT = new Printlist(e, rl); :}
		|
		expr:e
		{: RESULT = new Printlist(e); :}
		;

printlinelist ::=
		printlist:pl
		{: RESULT = new Printlinelist(pl); :}
		|
		{: RESULT = new Printlinelist(); :}
		;

expr ::= 
		name:n
		{: RESULT = new Expr(1, n); :}
		|
		ID:i OPENP CLOSEP
		{: RESULT = new Expr(2, i); :}
		|
		ID:i OPENP args:a CLOSEP
		{: RESULT = new Expr(3, i, a); :}
		|
		INTLIT:i
		{: RESULT = new Expr(4, i); :}
		|
		CHARLIT:c
		{: RESULT = new Expr(5, c); :}
		|
		STRLIT:s
		{: RESULT = new Expr(6, s); :}
		|
		FLOATLIT:f
		{: RESULT = new Expr(7, f); :}
		|
		TRUE
		{: RESULT = new Expr(8, true); :}
		|
		FALSE
		{: RESULT = new Expr(8, false); :}
		|
		OPENP expr:e CLOSEP
		{: RESULT = new Expr(9, e); :}
		|
		TILDE expr:e
		{: RESULT = new Expr(10, e); :}
		|
		MINUS expr:e
		{: RESULT = new Expr(11, e); :}
		|
		PLUS expr:e
		{: RESULT = new Expr(12, e); :}
		|
		OPENP type:t CLOSEP expr:e
		{: RESULT = new Expr(13, t, e); :}
		|
		binaryop:b
		{: RESULT = new Expr(14, b); :}
		|
		OPENP expr:e1 QUESTIONMARK expr:e2 COLON expr:e3 CLOSEP
		{: RESULT = new Expr(15, e1, e2, e3); :}
		;

binaryop ::= 
		expr:lhs MULT expr:rhs
		{: RESULT = new Binaryop(lhs, "*", rhs); :}
		|
		expr:lhs DIV expr:rhs
		{: RESULT = new Binaryop(lhs, "/", rhs); :}
		|
		expr:lhs PLUS expr:rhs
		{: RESULT = new Binaryop(lhs, "+", rhs); :}
		|
		expr:lhs MINUS expr:rhs
		{: RESULT = new Binaryop(lhs, "-", rhs); :}
		|
		expr:lhs LESSTHAN expr:rhs
		{: RESULT = new Binaryop(lhs, "<", rhs); :}
		|
		expr:lhs GREATERTHAN expr:rhs
		{: RESULT = new Binaryop(lhs, ">", rhs); :}
		|
		expr:lhs LEQ expr:rhs
		{: RESULT = new Binaryop(lhs, "<=", rhs); :}
		|
		expr:lhs GEQ expr:rhs
		{: RESULT = new Binaryop(lhs, ">=", rhs); :}
		|
		expr:lhs EQUALTO expr:rhs
		{: RESULT = new Binaryop(lhs, "==", rhs); :}
		|
		expr:lhs ANGLEB expr:rhs
		{: RESULT = new Binaryop(lhs, "<>", rhs); :}
		|
		expr:lhs OR expr:rhs
		{: RESULT = new Binaryop(lhs, "||", rhs); :}
		|
		expr:lhs AND expr:rhs
		{: RESULT = new Binaryop(lhs, "&&", rhs); :}
		;